apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: '../config/checkstyle/check.gradle'

apply plugin: 'com.github.dcendents.android-maven'
group='com.github.mskn73'

apply plugin: 'com.vanniktech.android.junit.jacoco'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 25
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  dexOptions {
    javaMaxHeapSize "4g"
  }

  sourceSets {
    //Dedicated directories for tests written in Kotlin
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }

  lintOptions {
    quiet true
    abortOnError false
    ignoreWarnings true
    disable 'InvalidPackage' //Some libraries have issues with this.
    disable 'OldTargetApi' //Lint gives this warning but SDK 20 would be Android L Beta.
    disable 'IconDensities' //For testing purpose. This is safe to remove.
    disable 'IconMissingDensityFolder' //For testing purpose. This is safe to remove.
  }


  buildTypes {
    debug {
      testCoverageEnabled true
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

//Ensure Kotlin will not be used in production code.
afterEvaluate {
  android.sourceSets.all { sourceSet ->
    if (!sourceSet.name.startsWith('test') || !sourceSet.name.startsWith('androidTest')) {
      sourceSet.kotlin.setSrcDirs([])
    }
  }
}

dependencies {
  def testDependencies = rootProject.ext.kachelibTestDependencies

  compile fileTree(dir: 'libs', include: ['*.jar'])
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  compile 'com.android.support:appcompat-v7:25.3.1'
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  compile 'com.google.code.gson:gson:2.4'
  compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.15'

  testCompile 'junit:junit:4.12'
  testCompile "org.robolectric:robolectric:3.2.1"
  testCompile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.6'
  testCompile 'org.jetbrains.kotlin:kotlin-test-junit:1.0.6'
  testCompile "com.nhaarman:mockito-kotlin:1.1.0"
  testCompile 'org.amshove.kluent:kluent:1.14'
}
repositories {
  mavenCentral()
}

checkstyle {
  configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
}
